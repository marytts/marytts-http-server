// FIXME: add test if voices exist in the class path !
buildscript {
    repositories {
        maven {
            url 'https://oss.jfrog.org/artifactory/oss-snapshot-local'
        }
        jcenter()
    }
}

plugins {
    id 'java'
    id "org.springframework.boot" version "2.0.1.RELEASE"
    id "org.asciidoctor.gradle.asciidoctor" version "1.5.1"
    id 'application'
}

apply plugin: 'io.spring.dependency-management' // FIXME: try to find away to avoid the "apply !"

mainClassName = 'marytts.http.Server'

version '0.1.0-SNAPSHOT'
status 'integration'

jar {
    baseName = rootProject.name
    version = version
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

repositories {
    jcenter()
    maven {
        url 'https://oss.jfrog.org/artifactory/oss-snapshot-local'
    }
}


sourceCompatibility = 1.8
targetCompatibility = 1.8


configurations {
    jaxDoclet
}
configurations.all {
   exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

def maryttsVersion = '6.0.1-SNAPSHOT'

dependencies {
    compile group:"org.json", name:"json", version:"20090211"

    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "logback-classic"
        exclude module: 'spring-boot-starter-logging'
    }

    compile ('org.springframework.boot:spring-boot-starter-log4j2')

    // compile group: 'de.dfki.mary', name: 'marytts-hsmm', version: maryttsVersion
    compile group: 'de.dfki.mary', name: 'marytts-common', version: maryttsVersion
    ['en'].each { locale ->
        runtime group: 'de.dfki.mary', name: "marytts-lang-$locale", version: maryttsVersion
    }

    // testCompile group: 'de.dfki.mary', name: 'marytts-hsmm', version: maryttsVersion
    testCompile group: 'de.dfki.mary', name: 'marytts-common', version: maryttsVersion
    ['en'].each { locale ->
        testCompile group: 'de.dfki.mary', name: "marytts-lang-$locale", version: maryttsVersion
    }
    runtime fileTree(dir: 'installed', include: '*.jar')

    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-web
    compile group: 'org.apache.logging.log4j', name: 'log4j-web', version: '2.6.2'


    //standard spring boot testing framework
    testCompile("org.springframework.boot:spring-boot-starter-test")

    //spring REST docs generator
    testCompile("org.springframework.restdocs:spring-restdocs-mockmvc")
}



/**********************************************************************************************
 ** Formatting
 ***********************************************************************************************/
task autoFormatCode() {
    def config = new File("astyle_marytts.conf")
    inputs.files config
    outputs.upToDateWhen { false }
    doLast {
        fileTree(dir: '.' , include: '**/*.java').each { java_file ->
		exec {
                commandLine "astyle", "--options=$config", java_file
            }
        }
    }
}


/**********************************************************************************************
 ** Documenting
 ***********************************************************************************************/
asciidoctor {
    sourceDir 'src/main/docs/asciidoc'
    outputDir "build/asciidoc"
    dependsOn test
    attributes \
      'snippets': file('build/snippets')
}



/**********************************************************************************************
 ** Running
 ***********************************************************************************************/
//pass agrument to Application class
bootRun {
    systemProperties = System.properties
    if ( project.hasProperty('args') ) {
        args project.args.split('\\s+')
    }
}
